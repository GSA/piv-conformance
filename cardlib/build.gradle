// set up junit 5 support for gradle
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.1.0'
    }
}

plugins {
    id 'org.ajoberstar.grgit' version '2.1.0'
    //id 'com.zyxist.chainsaw' version '0.1.3'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.junit.platform.gradle.plugin'

targetCompatibility = targetJavaVersion

logger.warn("target java version: " + targetCompatibility)
logger.warn("source java version: " + sourceCompatibility)

sourceSets {
    main {
        java {
            if(targetCompatibility != JavaVersion.VERSION_11)
                exclude '**/module-info.java'
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

import org.gradle.plugins.ide.eclipse.model.AccessRule

eclipse {
    classpath {
        file {
            whenMerged {
                def jre = entries.find { it.path.contains 'org.eclipse.jdt.launching.JRE_CONTAINER' }
                jre.accessRules.add(new AccessRule('0', 'javax/smartcardio/**'))
            }
        }
    }
}
    

//if(targetCompatibility != JavaVersion.VERSION_1_8)
//    javaModule.name = 'gov.gsa.pivconformance'

repositories {
    jcenter()
    maven { url 'https://repository.apache.org/snapshots' }
}

// custom task from
// https://stackoverflow.com/questions/39444908/how-to-create-an-html-report-for-junit-5-tests
// to get readable output for passing tests
configurations {
    junitXmlToHtml
}

dependencies {
    compile 'org.bouncycastle:bcprov-jdk15on:1.59+'
    compile 'org.bouncycastle:bcpkix-jdk15on:1.59+'
    compile 'commons-cli:commons-cli:1.4'
    compile 'commons-codec:commons-codec:1.11'
    compile 'com.payneteasy:ber-tlv:1.0-8'
    compile 'org.slf4j:slf4j-api:1.8+'
    compile 'ch.qos.logback:logback-classic:1.3+'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.21.0.1'

    testCompile 'org.bouncycastle:bcprov-jdk15on:1.59+'
    testCompile 'org.bouncycastle:bcpkix-jdk15on:1.59+'
    testCompile 'commons-cli:commons-cli:1.4'
    testCompile 'commons-codec:commons-codec:1.11'
    testCompile 'com.payneteasy:ber-tlv:1.0-8'
    testCompile 'org.slf4j:slf4j-api:1.8+'
    testCompile 'ch.qos.logback:logback-classic:1.3+'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.1.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-params:5.1.0'

    //shadow 'org.bouncycastle:bcprov-jdk15on:1.59'
    junitXmlToHtml 'org.apache.ant:ant-junit:1.9.7'

}

// Define the main class for the application jar that will be produced
/*mainClassName = 'gov.gsa.pivconformance.tools.ConformanceTestRunner'*/

jar {
    /*
    manifest {
        attributes 'Main-Class': mainClassName
    }*/
    println("Runtime dependencies:")
    doLast {
     configurations.runtime.each { println it }
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

// because the tests are also modular and we don't muck with the package exported by the library.
// An alternate approach may be preferable here, but this makes both gradle and the IDEs happy.
compileTestJava {
    //javaModule.name = 'gov.gsa.pivconformancetest'
}

// use junit platform for tests
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

// a place to configure junit platform plugin... not currently necessary, but it is very likely desirable
// to filter tests by tag
junitPlatform {
}


// get readable test reports since gradle eats the summary unless there's a failure
task generateHtmlTestReports {
    doLast {
        def reportsDir = new File(buildDir, 'test-reports')
        reportsDir.mkdirs()

        ant.taskdef(
            name: 'junitReport',
            classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
            classpath: configurations.junitXmlToHtml.asPath
        )

        ant.junitReport(todir: "$buildDir/test-results/junit-platform", tofile: "aggregated-test-results.xml") {
            fileset(dir: "$buildDir/test-results/junit-platform")
            report(format: 'frames', todir: reportsDir)
        }
    }
}

// make gradle check generate the reports
afterEvaluate {
    def junitPlatformTestTask = tasks.getByName('junitPlatformTest')
    generateHtmlTestReports.dependsOn(junitPlatformTestTask)
    check.dependsOn(generateHtmlTestReports)
}

// when producing a jar that is suitable for distribution using the shadow plugin,
// this will enbed a version properties file.
compileJava.doFirst {
        //options.compilerArgs = ['--add-modules', 'java.smartcardio']
        def git = org.ajoberstar.grgit.Grgit.open(dir: "${project.rootDir}/..")
        def commit = git.head()

        def commitId = commit.abbreviatedId
        def commitDate = commit.getDate()
        def buildDate = new Date()

        File resourcesDir = new File(project.getProjectDir(), 'src/main/resources/gov/gsa/pivconformance/utils')
        File propertiesFile = new File(resourcesDir, 'version.properties')

        // The project may not have any resources, so create the directories and file
        if(!propertiesFile.exists()) {
            resourcesDir.mkdirs()
            propertiesFile.createNewFile()
        }
        Properties versionProperties = new Properties()
        versionProperties.setProperty('git.commit.time', commitDate.format('dd.MM.yy HH.mm.ss'))
        versionProperties.setProperty('build.time', buildDate.format('dd.MM.yy HH.mm.ss'))
        versionProperties.setProperty('git.commit.id', commitId)
        versionProperties.setProperty('build.version', version)
        versionProperties.store(propertiesFile.newWriter(), null)
}

compileJava {
//    sourceCompatibility = '1.8'
    //targetCompatibility = '1.8'
}

task getDeps(type: Copy) {
  from sourceSets.main.runtimeClasspath
  into 'runtime/'
}

task installSource(type: Copy) {
    dependsOn sourceJar
    from sourceJar
    into "$projectDir/../tools/85b-swing-gui/lib"
}

task install(type: Copy) {
    dependsOn jar 
    from jar
    into "$projectDir/../tools/85b-swing-gui/lib"
}

